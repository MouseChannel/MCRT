#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_debug_printf : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_ARB_shader_clock : enable
// #extension GL_GOOGLE_cpp_style_line_direcitve :  require
#include "Data_struct.h"
#include "common.glsl"
#include "sampling.glsl"

layout(push_constant) uniform _PushContant_Ray
{
    PushContant_Ray pcRay;
};
layout(location = 0) rayPayloadEXT hitPayload prd;
layout(set = 0, binding = e_tlas) uniform accelerationStructureEXT topLevelAS;
layout(set = 0, binding = e_out_image, rgba32f) uniform image2D image;
layout(set = 1, binding = e_camera) uniform _camera_data
{
    Camera_data camera_data;
};
void main()
{
    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;

    vec2 texCoord = (vec2(gl_LaunchIDEXT.xy) + 0.5) / vec2(gl_LaunchSizeEXT.xy);
    vec3 ray_origin = camera_data.camera_pos.xyz;
    vec3 ray_dir = camera_data.camera_front.xyz + vec3(d, 0);

    uint rayFlags = gl_RayFlagsOpaqueEXT;
    float tMin = 0.001;
    float tMax = 10000.0;
    vec3 curWeight = vec3(1);
    vec3 hitValue = vec3(0);
    // if (gl_LaunchIDEXT.x == 1 && gl_LaunchIDEXT.y == 1)
    //     debugPrintfEXT("message from rgen %f %f %f\n", ray_origin.x, ray_origin.y, ray_origin.z);
    uint seed = tea(gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x + gl_LaunchIDEXT.x, int(clockARB()));
    prd.hitValue = vec3(0);
    prd.seed = seed;
    prd.depth = 0;
    prd.rayOrigin = ray_origin;
    prd.rayDirection = ray_dir;
    prd.weight = vec3(0);

    while (prd.depth++ < 10) {
        traceRayEXT(topLevelAS, // acceleration structure
                    rayFlags, // rayFlags
                    0xFF, // cullMask
                    0, // sbtRecordOffset
                    0, // sbtRecordStride
                    0, // missIndex
                    prd.rayOrigin, // ray origin
                    tMin, // ray min range
                    prd.rayDirection, // ray direction
                    tMax, // ray max range
                    0 // payload (location = 0)
        );
        hitValue += prd.hitValue * curWeight;
        curWeight *= prd.weight;
    }
    // imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.0));
    if (pcRay.frame > 0) {
        float a = 1.0f / float(pcRay.frame + 1);
        vec3 old_color = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).xyz;
        imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(mix(old_color, hitValue, a), 1.f));
    } else {
        // First frame, replace the value in the buffer
        imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.f));
    }
}